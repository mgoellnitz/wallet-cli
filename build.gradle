buildscript {
    repositories {
        mavenLocal()
        maven {
            url "https://cdn.lfrs.sl/repository.liferay.com/nexus/content/groups/public"
        }
        mavenCentral()
    }
    ext {
        projectVersion = '1.3.0-RELEASE'
        grpcVersion = '1.6.1'
        protobufVersion = '3.14.0'
        springCloudConsulVersion = '1.2.1.RELEASE'
    }

    dependencies {
        classpath "gradle.plugin.com.liferay:gradle-plugins-node:4.3.0"
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'com.google.protobuf' version '0.8.13'
   id 'io.franzbecker.gradle-lombok' version '4.0.0'
}

group 'Tron'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = 1.8
targetCompatibility = JavaVersion.VERSION_1_8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

compileJava.options*.compilerArgs = [
        "-Xlint:serial", "-Xlint:varargs", "-Xlint:classfile", "-Xlint:dep-ann",
        "-Xlint:divzero", "-Xlint:empty", "-Xlint:finally", "-Xlint:overrides",
        "-Xlint:path", "-Xlint:static", "-Xlint:try", "-Xlint:fallthrough",
        "-Xlint:deprecation", "-Xlint:unchecked", "-Xlint:-options"
]

repositories {
    mavenLocal()
    mavenCentral()
}

sourceSets {
    main {
        proto {
            srcDir 'src/main/protos'
        }
        java {
            srcDir 'src/main/gen'
            srcDir 'src/main/java'
        }
    }

}

lombok {
  version = "1.18.16"
  sha256 = "39f3922deb679b1852af519eb227157ef2dd0a21eec3542c8ce1b45f2df39742"
}

dependencies {
    testCompile 'junit:junit:4.13.1'
    compile 'com.beust:jcommander:1.78'
    //compile 'org.slf4j:slf4j-api:1.7.30'
    compile 'org.slf4j:jcl-over-slf4j:1.7.30'
    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile 'com.maxmind.geoip2:geoip2:2.15.0'

    // google grpc
    compile 'io.grpc:grpc-netty:1.34.1'
    compile 'io.grpc:grpc-protobuf:1.34.1'
    compile 'io.grpc:grpc-stub:1.34.1'

    compile 'com.googlecode.protobuf-java-format:protobuf-java-format:1.4'
    compile 'com.madgag.spongycastle:core:1.58.0.0'
    compile 'com.madgag.spongycastle:prov:1.58.0.0'
    compile 'com.typesafe:config:1.4.1'
    compile 'com.google.code.findbugs:jsr305:3.0.2'
    compile "org.springframework.cloud:spring-cloud-starter-consul-discovery:${springCloudConsulVersion}"
    compile 'org.apache.commons:commons-collections4:4.4'
    compile 'org.apache.commons:commons-lang3:3.11'
    compile 'com.google.api.grpc:googleapis-common-protos:0.0.3'
    compile 'com.alibaba:fastjson:1.2.75'

    compile 'commons-io:commons-io:2.8.0'
    compile 'org.projectlombok:lombok:1.18.16'

    compile 'org.jline:jline:3.18.0'
}

protobuf {
    generatedFilesBaseDir = "$projectDir/src/"
    protoc {
        artifact = "com.google.protobuf:protoc:3.14.0"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.9.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    outputSubDir = "gen"
                }
            }
        }
        all()*.plugins {
            grpc {
                outputSubDir = "gen"
            }
        }
    }
}

run {
    standardInput = System.in
    mainClassName = 'org.tron.walletcli.Client'
}

shadowJar {
    baseName = 'wallet-cli'
    classifier = null
    version = null
}
